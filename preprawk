#!/usr/bin/awk -f
# vim: ft=awk tw=80 sw=3 ts=3 sts=3
#
#-------------------------------------------------------------------------------
# Further working on creating a bash preprocessor.
#
# Features it should have:
#  * "Topics" that can be default hidden, and must be `subscribed` to, or
#    default shown and must be `unsubscribed` from
#  * Dot syntax for intermediate nameref expansion
#  * Optional strip comments, empty newlines
#
#-------------------------------------------------------------------------------

BEGIN {
   # Expecting user to pass in either/both:
   #> SUBSCRIBE=""
   #> UNSUBSCRIBE=""
   #
   # Should be a comma delimited string of topics to subscribe to, or
   # unsubscribe from. Example:
   #> awk -f preprawk -v SUBSCRIBE="TESTS" -v UNSUBSCRIBE="NOISE"

   # Kinda just for convenience and legibility.
   true  = 1
   false = 0

   # topics to subscribe to
   delete SUB[0]
   delete _SUB[0]
   split(SUBSCRIBE, _SUB, ",")
   for (i in _SUB) gsub(/[[:space:]]/, "", _SUB[i])
   for (i in _SUB) if (_SUB[i]) SUB[_SUB[i]] = true

   # topics to unsubscribe from
   delete UNSUB[0]
   delete _UNSUB[0]
   split(UNSUBSCRIBE, _UNSUB, ",")
   for (i in _UNSUB) gsub(/[[:space:]]/, "", _UNSUB[i])
   for (i in _UNSUB) if (_UNSUB[i]) UNSUB[_UNSUB[i]] = true

   delete CUR_SUBS[0]      # currently subscribed to...
   delete CUR_UNSUBS[0]    # currently unsubscribed from...
}


function clear(arr,   i) {
   for (i in arr) delete arr[i]
}


function subscribed(   i, j) {
   for (i in CUR_SUBS) {
      for (j in SUB) {
         if (CUR_SUBS[j]) {
            return true
         }
      }
   }
   return false
}


function unsubscribed(   i, j) {
   for (i in CUR_UNSUBS) {
      for (j in UNSUB) {
         if (CUR_UNSUBS[j]) {
            return true
         }
      }
   }
   return false
}


function active_sub(   i) {
   for (i in CUR_SUBS) return true
   return false
}


function active_unsub(   i) {
   for (i in CUR_UNSUBS) return true
   return false
}

#                                 subscribe to
#-------------------------------------------------------------------------------
# match:  start of topic subscription block
#
/^\s*#>\s+SUB\s+[[:alpha:]_][[:alnum:]_]*/ {
   sub(/^\s*#>\s+SUB\s+/, "", $0)
   gsub(/,/, " ", $0)
   split($0, topics, " ")
   for (i in topics) {
      CUR_SUBS[topics[i]] = true
   }
   next
}

# match:  end of topic subscription block
#
/^\s*#>\s+END\s+SUB\s+[[:alpha:]_][[:alnum:]_]*/ {
   sub(/^\s*#>\s+SUB\s+/, "", $0)
   gsub(/,/, " ", $0)
   split($0, topics, " ")
   for (i in topics) {
      delete CUR_SUBS[topics[i]]
   }
   next
}

# match:  end of all current subscriptions
#
/^\s*#>\s*END\s+SUBS\s*$/ {
   clear(CUR_SUBS) ; next
}

#                               unsubscribe from
#-------------------------------------------------------------------------------
# match:  start of topic unsubscription block
#
/^\s*#>\s+UNSUB\s+[[:alpha:]_][[:alnum:]_]*/ {
   sub(/^\s*#>\s+UNSUB\s+/, "", $0)
   gsub(/,/, " ", $0)
   split($0, topics, " ")
   for (i in topics) {
      CUR_UNSUBS[topics[i]] = true
   }
   next
}

# match:  end of topic unsubscription block
#
/^\s*#>\s+END\s+UNSUB\s+[[:alpha:]_][[:alnum:]_]*/ {
   sub(/^\s*#>\s+UNSUB\s+/, "", $0)
   gsub(/,/, " ", $0)
   split($0, topics, " ")
   for (i in topics) {
      delete CUR_UNSUBS[topics[i]]
   }
   next
}

# match:  end of all current unsubscriptions
#
/^\s*#>\s*END\s+UNSUBS\s*$/ {
   clear(CUR_UNSUBS) ; next
}


# match:  dot notation for accessing nested array
#         elements, creates intermediate namerefs
#
match($0, /\$\{[[:alpha:]_][[:alnum:]_]*(\.[[:alpha:]_][[:alnum:]_]*)+\}/) {
   text = substr($0, RSTART, RLENGTH)
   sub(/^\$\{/, "", text) ; sub(/\}$/, "", text)
   split(text, words, ".")

   match($0, /^\s*/)
   column = RLENGTH
   indent = ""

   while (column) {
      indent = indent " "
      column = column - 1
   }

   print indent "unset '__'"
   print indent "declare -n __=\"${"  words[1]  "}\""

   len = 0
   for (w in words) len = len + 1

   for (idx=2; idx<len; ++idx) {
      print indent "declare -n __=\"${__['"  words[idx]  "']}\""
   }

   repl = "${__['"  words[len]  "']}"

   sub(/\$\{[[:alpha:]_][[:alnum:]_]*(\.[[:alpha:]_][[:alnum:]_]*)+\}/, repl)
}

#                                    print
#-------------------------------------------------------------------------------
/^#!/ && (FNR == 1) && (FNR != NR) { next }  # Skips non-first `#!`
{ sub(/\s*$/, "") }                          # Clean up EOL whitespace

STRIP_COMMENTS && /^\s*#/ { next }           # Skip comments.
STRIP_COMMENTS { sub(/\s+#.*$/, "") }        # Strip in-line comments.
STRIP_NEWLINES && /^\s*$/ { next }           # Skip empty lines.

active_sub()   && ! subscribed() { next }
active_unsub() && unsubscribed() { next }

{ print }
